.版本 2

.程序集 RGC对话框
.程序集变量 对话框文字, Age_文字
.程序集变量 内容文字, Age_文字
.程序集变量 对话框, RGC对话框, , "0"
.程序集变量 矢量按钮, Age_矢量按钮
.程序集变量 对话框透明度, 整数型

.子程序 RGC对话框初始化

对话框文字.创建 (“黑体”, 30 × 分辨率缩放倍率, 假, 假, 真, #颜色_白)
内容文字.创建 (“黑体”, 20 × 分辨率缩放倍率, 假, 假, 真, #颜色_白)
矢量按钮.创建 (分辨率.窗口宽度 － 200 × 分辨率缩放倍率, 分辨率.窗口高度 － 100 × 分辨率缩放倍率, 100 × 分辨率缩放倍率, 50 × 分辨率缩放倍率, #颜色_黑, #颜色_灰, #颜色_暗灰, #颜色_黑, &执行)

.子程序 显示对话框
.参数 标题, 文本型
.参数 内容, 文本型
.局部变量 临时标题, RGC对话框

临时标题.标题 ＝ 标题
临时标题.文字 ＝ 内容
加入成员 (对话框, 临时标题)

.子程序 RGC对话框逻辑

.如果真 (RGC对话框开关 ＝ 真)
    矢量按钮.按钮逻辑更新 ()
.如果真结束
.如果 (取数组成员数 (对话框) ＝ 0)
    RGC对话框开关 ＝ 假
.否则
    RGC对话框开关 ＝ 真
.如果结束


.子程序 RGC对话框渲染

.如果 (RGC对话框开关 ＝ 真)
    .如果 (对话框透明度 ＋ 帧率数据处理 (30) ≥ 255)
        对话框透明度 ＝ 255
    .否则
        对话框透明度 ＝ 对话框透明度 ＋ 帧率数据处理 (30)
    .如果结束

.否则
    .如果 (对话框透明度 － 帧率数据处理 (30) ≤ 0)
        对话框透明度 ＝ 0
    .否则
        对话框透明度 ＝ 对话框透明度 － 帧率数据处理 (30)
    .如果结束

.如果结束
.如果真 (对话框透明度 ≠ 0)
    对话框文字.置透明度 (对话框透明度)
    内容文字.置透明度 (对话框透明度)
    .如果 (取数组成员数 (对话框) ＝ 0)
        对话框文字.显示 (20 × 分辨率缩放倍率, 20 × 分辨率缩放倍率, “”)
        内容文字.显示 (20 × 分辨率缩放倍率, 70 × 分辨率缩放倍率, “”)
    .否则
        对话框文字.显示 (20 × 分辨率缩放倍率, 20 × 分辨率缩放倍率, 对话框 [1].标题)
        内容文字.显示 (20 × 分辨率缩放倍率, 70 × 分辨率缩放倍率, 对话框 [1].文字)
    .如果结束
    .如果 (对话框透明度 ≠ 255)

    .否则
        矢量按钮.置颜色 (#颜色_黑, #颜色_灰, #颜色_暗灰)
        矢量按钮.显示 ()
    .如果结束

    对话框文字.显示 (分辨率.窗口宽度 － 180 × 分辨率缩放倍率, 分辨率.窗口高度 － 90 × 分辨率缩放倍率, “确定”)
    鼠标显示 ()
.如果真结束


.子程序 执行

.如果 (取数组成员数 (对话框) ＝ 0)

.否则
    删除成员 (对话框, 1, 1)
.如果结束

