.版本 2

.程序集 特殊选项对话框
.程序集变量 对话框透明度, 整数型
.程序集变量 内容文字, Age_文字
.程序集变量 标题文字, Age_文字
.程序集变量 文字, 文本型, , "0"

.子程序 按钮设定对话框初始化

内容文字.创建 (“黑体”, 30 × 分辨率缩放倍率, 假, 假, 真, #颜色_白)
标题文字.创建 (“黑体”, 40 × 分辨率缩放倍率, 假, 假, 真, #颜色_白)
加入成员 (文字, “按下你想设定的按键”)
加入成员 (文字, “(按下鼠标右键取消设定)”)

.子程序 按钮设定对话框渲染
.局部变量 n, 整数型

.如果真 (对话框透明度 ≠ 0)
    引擎.画实心矩形 (0, 0, 分辨率.窗口宽度, 300 × 分辨率缩放倍率, ARGB (对话框透明度 × 0.8, 0, 0, 0))
    标题文字.显示 (30 × 分辨率缩放倍率, 30 × 分辨率缩放倍率, “按键设定”)
    .计次循环首 (取数组成员数 (文字), n)
        内容文字.显示 (分辨率.窗口宽度 ÷ 2 － 内容文字.取宽度 (文字 [n]) ÷ 2, (150 － 30 ÷ 2 ＋ 30 × (n － 1)) × 分辨率缩放倍率, 文字 [n])
    .计次循环尾 ()
.如果真结束
鼠标显示 ()

.子程序 按钮设定对话框逻辑

.如果 (更换按键事件 ＝ 真)
    停止渲染 ＝ 真
.否则
    停止渲染 ＝ 假
.如果结束
.如果 (更换按键事件 ＝ 真)
    .如果 (对话框透明度 ＋ 帧率数据处理 (30) ≥ 255)
        对话框透明度 ＝ 255

    .否则
        对话框透明度 ＝ 对话框透明度 ＋ 帧率数据处理 (30)
    .如果结束

.否则
    .如果 (对话框透明度 － 帧率数据处理 (30) ≤ 0)
        对话框透明度 ＝ 0
    .否则
        对话框透明度 ＝ 对话框透明度 － 帧率数据处理 (30)
    .如果结束

.如果结束
内容文字.置颜色 (ARGB (对话框透明度, 255, 255, 255))
标题文字.置颜色 (ARGB (对话框透明度, 255, 255, 255))
.如果真 (引擎.键_取按下 (#鼠标右键))
    更换按键事件 ＝ 假
.如果真结束



