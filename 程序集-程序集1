.版本 2

.程序集 程序集1
.程序集变量 柔化滤镜, 整数型
.程序集变量 游戏渲染区, 整数型
.程序集变量 游戏渲染区精灵, Age_精灵
.程序集变量 渲染区震荡特效, 震荡特效, , "0"

.子程序 _启动子程序, 整数型, , 本子程序在程序启动后最先执行
.局部变量 屏幕, Tp_窗口
.局部变量 刷新率, 整数型

检查文件 ()
载入 (窗口1, , 假)
BASS_Init (-1, 44100, 4, 窗口句柄, 0)
屏幕.逻辑函数 ＝ &逻辑
屏幕.渲染函数 ＝ &渲染
屏幕.退出函数 ＝ &退出程序
屏幕.后台运行 ＝ 真
屏幕.隐藏鼠标 ＝ 真
分辨率设定 ()
屏幕.高 ＝ 分辨率.窗口高度
屏幕.宽 ＝ 分辨率.窗口宽度
屏幕.渲染遗失函数 ＝ &渲染区遗失
设定读取 ()
屏幕.标题 ＝ #游戏标题 ＋ “ ” ＋ #版本号
遮罩切换_无执行 (“UI”, 10, 真)
' 遮罩切换_无执行 (“计分”, 10, 真)
屏幕.帧率 ＝ 200
引擎.创建 (屏幕, 真)
游戏渲染区 ＝ 引擎.创建渲染区 (屏幕.宽, 屏幕.高, 假)
游戏渲染区精灵.创建 (引擎.取渲染区纹理 (游戏渲染区), 0, 0, 屏幕.宽, 屏幕.高)
创建时钟 (窗口句柄, 1, 1, &取播放时间)
写到文件 (取运行目录 () ＋ “\Skins\Logo.ico”, #游戏LOGO)
引擎.置图标 (取运行目录 () ＋ “\Skins\Logo.ico”)
音乐BPM ＝ 120
调试信息.创建 (“宋体”, 13, 假, 假, 假, #颜色_白)

按键初始化 ()
游戏初始化 ()
UI初始化 ()
界面类初始化 ()
设置界面初始化 ()
计分界面初始化 ()
置随机数种子 ()
引擎.启动 ()
引擎.销毁 ()
返回 (0)  ' 可以根据您的需要返回任意数值

.子程序 按键初始化

按键红1 ＝ #X键
按键红2 ＝ #N键
按键蓝1 ＝ #Z键
按键蓝2 ＝ #M键
暂停按键 ＝ #Esc键

.子程序 分辨率设定

分辨率.窗口高度 ＝ 600
分辨率.窗口宽度 ＝ 1024
分辨率.Bar缩放 ＝ 2
分辨率.logo位置 ＝ 分辨率.窗口宽度 ÷ 2
分辨率.弹出太鼓1 ＝ 1050
分辨率.弹出太鼓2 ＝ 0.00025
分辨率.弹出太鼓3 ＝ 40
分辨率.遮罩缩放X ＝ 6
分辨率.遮罩缩放Y ＝ 6

.判断开始 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Display”, “Resolution”, ) ＝ “1024*600”)
    分辨率.窗口高度 ＝ 600
    分辨率.窗口宽度 ＝ 1024
    分辨率.Bar缩放 ＝ 2
    分辨率.logo位置 ＝ 分辨率.窗口宽度 ÷ 2
    分辨率.弹出太鼓1 ＝ 1050
    分辨率.弹出太鼓2 ＝ 0.00025
    分辨率.弹出太鼓3 ＝ 40
    分辨率.遮罩缩放X ＝ 6
    分辨率.遮罩缩放Y ＝ 6
.判断 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Display”, “Resolution”, ) ＝ “800*600”)
    分辨率.窗口高度 ＝ 600
    分辨率.窗口宽度 ＝ 800
    分辨率.Bar缩放 ＝ 1
    分辨率.logo位置 ＝ 分辨率.窗口宽度 ÷ 2
    分辨率.弹出太鼓1 ＝ 1050
    分辨率.弹出太鼓2 ＝ 0.00032
    分辨率.弹出太鼓3 ＝ 10
    分辨率.遮罩缩放X ＝ 4
    分辨率.遮罩缩放Y ＝ 4
.默认

.判断结束


.子程序 退出程序, 文本型

.如果真 (是否已创建 (窗口1) ＝ 真)
    窗口1.销毁 ()
.如果真结束
返回 (“true”)

.子程序 设定读取

.如果 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Debug”, “Debug”, ) ＝ “1”)
    调试信息显示 ＝ 真
.否则

.如果结束
.如果 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Game”, “Auto”, ) ＝ “1”)
    自动模式 ＝ 真
.否则

.如果结束
.如果 (读配置项 (取运行目录 () ＋ “\Config.ini”, “MOD”, “LightMod”, ) ＝ “1”)
    MOD开关.灯光MOD ＝ 真
.否则

.如果结束
.如果 (读配置项 (取运行目录 () ＋ “\Config.ini”, “effect”, “FlashH”, ) ＝ “1”)
    开关.魂满闪光 ＝ 真
.否则

.如果结束
.如果 (读配置项 (取运行目录 () ＋ “\Config.ini”, “effect”, “Shock”, ) ＝ “1”)
    视频开关.UIGOGO特效 ＝ 真
.否则

.如果结束



全局OFFSET ＝ 到小数 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Game”, “Offset”, ))
全局音量 ＝ 到整数 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Game”, “Volume”, ))
音效音量 ＝ 到整数 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Game”, “SVolume”, ))
音乐音量 ＝ 到整数 (读配置项 (取运行目录 () ＋ “\Config.ini”, “Game”, “MVolume”, ))


.子程序 逻辑, 逻辑型

.如果 (当前窗口 ＝ “太鼓游戏”)
    游戏逻辑 ()
.否则

.如果结束
.如果 (当前窗口 ＝ “UI”)
    UI逻辑 ()
.否则

.如果结束
.如果 (当前窗口 ＝ “Setting”)
    设置界面逻辑 ()
.否则

.如果结束
.如果 (当前窗口 ＝ “计分”)
    计分界面逻辑 ()
.否则

.如果结束

鼠标取坐标 ()
返回 (假)


.子程序 渲染, 逻辑型

.如果真 (MOD开关.灯光MOD ＝ 真)
    灯光渲染区渲染 ()
.如果真结束
.如果真 (当前窗口 ＝ “太鼓游戏”)
    动画视频渲染区渲染 ()
.如果真结束

引擎.渲染开始 (游戏渲染区)
引擎.清屏 ()
.如果 (当前窗口 ＝ “太鼓游戏”)
    游戏渲染 ()
.否则

.如果结束
.如果 (当前窗口 ＝ “UI”)
    ui渲染 ()
.否则

.如果结束
.如果 (当前窗口 ＝ “Setting”)
    设置界面渲染 ()
.否则

.如果结束
.如果 (当前窗口 ＝ “计分”)
    计分界面渲染 ()
.否则

.如果结束


引擎.渲染结束 ()


引擎.渲染开始 ()
引擎.清屏 ()
游戏渲染区精灵.置中心 (0, 0)
游戏渲染区精灵.置透明度 (255)
游戏渲染区精灵.显示 (0, 0)
渲染区特效处理并渲染 ()
.如果 (调试信息显示 ＝ 真)
    调试信息显示 ()
.否则

.如果结束
鼠标显示 ()
遮盖显示 ()
引擎.渲染结束 ()
返回 (假)



.子程序 渲染区遗失, 逻辑型

灯光渲染区遗失 ()
视频渲染区遗失 ()
游戏渲染区精灵.置纹理 (引擎.取渲染区纹理 (游戏渲染区))
返回 (假)

.子程序 调试信息显示
.局部变量 系统环境, 文本型
.局部变量 显示文字, 文本型

.判断开始 (系统_取版本 () ＝ 5.1)
    系统环境 ＝ “Windows XP”
.判断 (系统_取版本 () ＝ 6)
    系统环境 ＝ “Windows Vista”
.判断 (系统_取版本 () ＝ 6.1)
    系统环境 ＝ “Windows 7”
.判断 (系统_取版本 () ＝ 6.2)
    系统环境 ＝ “Windows 8”
.默认

.判断结束
调试信息.置颜色 (#颜色_黑)
显示文字 ＝ “当前FPS：” ＋ 到文本 (引擎.取FPS ()) ＋ #换行符 ＋ “窗口分辨率：” ＋ 到文本 (引擎.取窗口宽 ()) ＋ “× ” ＋ 到文本 (引擎.取窗口高 ()) ＋ #换行符 ＋ “引擎最后帧时间：” ＋ 到文本 (引擎.取最后帧时间 ()) ＋ #换行符 ＋ “游戏已运行：” ＋ 到文本 (取整 (引擎.取运行时间 ())) ＋ “秒” ＋ #换行符 ＋ “运行环境：” ＋ 系统环境 ＋ #换行符 ＋ “鼠标坐标：X.” ＋ 到文本 (鼠标坐标.x) ＋ “ Y.” ＋ 到文本 (鼠标坐标.y)
调试信息.显示 (11, 10, 显示文字)
调试信息.显示 (10, 11, 显示文字)
调试信息.显示 (9, 10, 显示文字)
调试信息.显示 (10, 9, 显示文字)
调试信息.置颜色 (#颜色_白)
调试信息.显示 (10, 10, 显示文字)

.子程序 显示文字
.参数 文字, 文本型
.参数 X, 整数型
.参数 Y, 整数型

调试信息.显示 (X, Y, 文字)


.子程序 渲染区特效处理并渲染
.局部变量 n, 整数型

.计次循环首 (取数组成员数 (渲染区震荡特效), n)
    .如果 (n ＞ 取数组成员数 (渲染区震荡特效))

    .否则
        .如果 (渲染区震荡特效 [n].透明度 － 10 ≤ 0)
            渲染区震荡特效 [n].透明度 ＝ 0
        .否则
            渲染区震荡特效 [n].透明度 ＝ 渲染区震荡特效 [n].透明度 － 10
        .如果结束
        渲染区震荡特效 [n].缩放 ＝ 渲染区震荡特效 [n].缩放 ＋ 10
        .如果 (渲染区震荡特效 [n].透明度 ＝ 0)
            删除成员 (渲染区震荡特效, n, 1)
        .否则

        .如果结束

    .如果结束

.计次循环尾 ()

游戏渲染区精灵.置中心 (游戏渲染区精灵.取宽度 () ÷ 2, 游戏渲染区精灵.取高度 () ÷ 2)
.计次循环首 (取数组成员数 (渲染区震荡特效), n)
    游戏渲染区精灵.置透明度 (渲染区震荡特效 [n].透明度)
    游戏渲染区精灵.显示高级 (分辨率.窗口宽度 ÷ 2, 分辨率.窗口高度 ÷ 2, 0, 渲染区震荡特效 [n].缩放 ÷ 1000, 渲染区震荡特效 [n].缩放 ÷ 1000)
.计次循环尾 ()


.子程序 震荡一下
.局部变量 临时震荡, 震荡特效

.如果真 (视频开关.UIGOGO特效 ＝ 真)
    临时震荡.缩放 ＝ 1000
    临时震荡.透明度 ＝ 150
    加入成员 (渲染区震荡特效, 临时震荡)
.如果真结束

